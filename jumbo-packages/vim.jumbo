pkgname=vim
pkgver=7.3.798
pkgrel=1
_versiondir=vim73
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
depends=('xz' 'ncurses' 'python' 'perl') # missdepends=('ruby' 'lua' 'gpm')
sources=("${JUMBO_REPO}/packages/${pkgname}/${pkgname}-${pkgver}.tar.xz"
         "${JUMBO_REPO}/packages/${pkgname}/pythoncomplete.vim"
         "${JUMBO_REPO}/packages/${pkgname}/archlinux.vim"
         "${JUMBO_REPO}/packages/${pkgname}/vimrc-v2")
md5sums=('e4a759fdb7cf91d91fd4f8e4f44d5d9c'
         '6e7adfbd5d26c1d161030ec203a7f243'
         '10353a61aadc3f276692d0e17db1478e'
         '6aef60b424afe1052b580ece9f2e6653')

jumbo_install() {
  cd "${srcdir}"
  xz -c -d "${pkgname}-${pkgver}.tar.xz" | tar -x

  cd "${srcdir}/${pkgname}-${pkgver}"

  echo "#define SYS_VIMRC_FILE \"${JUMBO_ROOT}/etc/vimrc\"" >> src/feature.h

  vi_cv_path_python="${JUMBO_ROOT}/bin/python" \
    ./configure \
    --prefix="${JUMBO_ROOT}" \
    --localstatedir="${JUMBO_ROOT}/var/lib/vim" \
    --with-features=huge \
    --disable-gpm \
    --enable-acl \
    --with-x=no \
    --disable-gui \
    --enable-multibyte \
    --enable-cscope \
    --enable-netbeans \
    --enable-perlinterp \
    --enable-pythoninterp \
    --disable-python3interp \
    --disable-rubyinterp \
    --disable-luainterp \
    --without-local-dir

  make
  make DESTDIR="${pkgdir}" install

  # Don't forget logtalk.dict
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/runtime/ftplugin/logtalk.dict" \
    "${pkgdir}/${JUMBO_ROOT}/share/vim/${_versiondir}/ftplugin/logtalk.dict"

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    "${pkgdir}/${JUMBO_ROOT}/share/vim/${_versiondir}/filetype.vim"

  # make Aaron happy
  install -Dm644 "${srcdir}/pythoncomplete.vim" \
    "${pkgdir}/${JUMBO_ROOT}/share/vim/${_versiondir}/autoload/pythoncomplete.vim"

  # rc files
  install -Dm644 "${srcdir}/vimrc-v2" "${pkgdir}/${JUMBO_ROOT}/etc/vimrc"
  install -Dm644 "${srcdir}/archlinux.vim" \
    "${pkgdir}/${JUMBO_ROOT}/share/vim/vimfiles/archlinux.vim"

  # rgb.txt file
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/runtime/rgb.txt" \
    "${pkgdir}/${JUMBO_ROOT}/share/vim/${_versiondir}/rgb.txt"
}

# vim:set ft=sh ts=2 sw=2 et:
