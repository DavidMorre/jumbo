pkgname=python
pkgver=2.7.3
pkgrel=5
_pybasever=2.7
pkgdesc="A high-level scripting language"
depends=('sqlite3' 'zlib' 'expat' 'libffi' 'bzip2' 'openssl') # missdepends=('gdbm')
sources=("${JUMBO_REPO}/packages/${pkgname}/Python-${pkgver}.tar.bz2"
         "${JUMBO_REPO}/packages/${pkgname}/000-damn-usr-local-die-die-die.patch")
md5sums=('c57477edd6d18bd9eeca2f21add73919'
         'c272c36f5d1709f55ad6e67a731c1d4e')

jumbo_install() {
  cd "${srcdir}/Python-${pkgver}"

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite3 module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  # Jun Huang: use /usr/bin/env instead
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/env python|" Lib/cgi.py

  # Jun Huang: fix to never use the libraries in /usr/local/
  patch -Np1 -i ../000-damn-usr-local-die-die-die.patch

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  unset MAKEFLAGS
  export OPT="-I. -IInclude -I./Include ${CFLAGS}"
  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS
  export LDFLAGS="-L. ${LDFLAGS}"
  ./configure --prefix="${JUMBO_ROOT}" --enable-shared --with-threads \
              --enable-unicode=ucs4 --with-system-expat --with-system-ffi
  make
  make DESTDIR="${pkgdir}" install

  ln -sf python${_pybasever}        "${pkgdir}/${JUMBO_ROOT}/bin/python2"
  ln -sf python${_pybasever}-config "${pkgdir}/${JUMBO_ROOT}/bin/python2-config"
  ln -sf python${_pybasever}.1      "${pkgdir}/${JUMBO_ROOT}/share/man/man1/python2.1"

  ln -sf ../../libpython${_pybasever}.so \
    "${pkgdir}/${JUMBO_ROOT}/lib/python${_pybasever}/config/libpython${_pybasever}.so"

  mv "${pkgdir}/${JUMBO_ROOT}/bin/smtpd.py" "${pkgdir}/${JUMBO_ROOT}/lib/python${_pybasever}/"

  # some useful "stuff"
  install -dm755 "${pkgdir}"/${JUMBO_ROOT}/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    "${pkgdir}/${JUMBO_ROOT}/lib/python${_pybasever}/Tools/i18n/"
  install -m755 Tools/scripts/{README,*py} \
    "${pkgdir}/${JUMBO_ROOT}/lib/python${_pybasever}/Tools/scripts/"

  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" \
    "${pkgdir}/${JUMBO_ROOT}/lib/python${_pybasever}/config/Makefile"

  # check if there's something wrong
  if find "${pkgdir}/${JUMBO_ROOT}" | grep "failed.so"
  then
    return 1
  else
    return 0
  fi 
}

# vim:set ft=sh ts=2 sw=2 et:
